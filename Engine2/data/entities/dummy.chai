var dummyScript = EntityScript();

global GruntActionType_None = 0;
global GruntActionType_Attack = 1;

class GruntState 
{
    var actionType;
    // ivec2 
    var targetHex;

    var hasActedThisRound;

    def GruntState(entity)
    {
        this.actionType = GruntActionType_None;
        this.hasActedThisRound = false;
    }
}

def gruntAttack(target) {

    var instigator = game.getSelectedEntity();

    if(instigator.is_var_null || target.is_var_null)
    {
        return;
    }

    instigator.scriptState.targetHex = target.tileIndex;

    instigator.attackPointsLeft -= 1;
    instigator.movePointsLeft = 0;
    instigator.turnTowards(target.tileIndex);
    instigator.playAction("chop");
    instigator.scriptState.actionType = GruntActionType_Attack;
    game.beginCustomAction();
}

dummyScript.setCreateState(fun(entity){
    return GruntState(entity);
});

dummyScript.setOnActionTrigger(fun(entity, action, trigger){

    var targetEntity = game.entityAtHex(LI_Unit, entity.scriptState.targetHex);

    var isShotTrigger = action == "chop" && trigger == "hit";
    var isAttacking = entity.scriptState.actionType == GruntActionType_Attack;
    var isTargetValid = !targetEntity.is_var_null;

    if(isShotTrigger && isAttacking && isTargetValid)
    {
        var attackMeta = calculateAttackMeta(entity, targetEntity);

        game.applyDamage(targetEntity, entity, attackMeta.targetDamage);

        if(attackMeta.instigatorDamage > 0.0)
        {
            game.applyDamage(entity,targetEntity, attackMeta.instigatorDamage);
        }
    }
});

dummyScript.setUpdateCustomAction(fun(entity, time) {

    if(entity.health > 0.0)
    {
        if(entity.scriptState.actionType == GruntActionType_Attack)
        {
            if(!entity.isActionPlaying("chop"))
            {
                entity.scriptState.actionType = GruntActionType_None;
                game.endCustomAction();
                game.resolveLocalEntity();
            }

            return;
        }
    }
    // we probably dont wanna get stuck in an invalid state here, so this line prob doesnt hurt
    game.endCustomAction();
});

dummyScript.setOnHit(fun(entity, instigator, damage){

    entity.turnTowards(instigator.tileIndex);

    entity.health -= damage;

    if(entity.health < 0.0)
    {
        game.killEntity(entity);
    }
});

dummyScript.setOnTargetChanged(fun(entity, newTarget){
    entity.scriptState.targetHex = newTarget;
});

dummyScript.setOnTargetClicked(fun(entity){
    game.endTargeting();

    var targetEntity = game.entityAtHex(LI_Unit, entity.scriptState.targetHex);
    if(targetEntity.is_var_null){
        return;
    }

    var attackDistance = hexDistance(targetEntity.tileIndex, entity.tileIndex);
    if(attackDistance > entity.specification.attackRange)
    {
        return;
    }

    gruntAttack(targetEntity);
});

dummyScript.setDrawUI(fun(entity, ui){
    var canAttack = entity.attackPointsLeft > 0 && entity.grugCanAttack;
    ui.beginStackH("gruntH1", 40.0)
    if(ui.gameGridButton("gruntAttack", "gameUi.attack", "Attack enemy", canAttack))
    {
        game.beginTargeting();
    }

    ui.endStackH();
});

dummyScript.setOnTurnStart(fun(entity){
    entity.scriptState.hasActedThisRound = false;
});

dummyScript.setPlayerRelevant(fun(entity){

    var canMove =  entity.movePointsLeft > 0 && entity.grugCanMove;
    var canAttack = entity.attackPointsLeft > 0 && entity.grugCanAttack;

    return canMove || canAttack;
});


dummyScript.setGrugRelevant(fun(entity){
    // grug no want be left out, so grug always say grug relevant
    
    return true;
});

dummyScript.setGrugTick(fun(entity, seconds){
    var turnState = game.getTurnState();

    if(turnState == TS_Unlocked) {

        if(entity.scriptState.hasActedThisRound){
            return false;
        }

        var target = game.grugAttackTarget();
        var canAttack = false;

        if(!target.is_var_null){
            var attackMeta = calculateAttackMeta(entity, target);
            canAttack = attackMeta.targetDamage > 0.0;
        }

        if(canAttack && entity.attackPointsLeft > 0){
            var attackMovePoints = game.grugNumAttackMovePoints();
            if(attackMovePoints > 0)
            {
                var moveTo = game.grugAttackMoveLocation();
                game.moveSelectedEntityTo(moveTo);
                return true;
            }
            else 
            {
                gruntAttack(target);
                return true;
            }
        } else if(entity.movePointsLeft > 0) {
            var moveTo = game.grugMoveLocation();
            if(moveTo.x == entity.tileIndex.x && moveTo.y == entity.tileIndex.y)
            {
                return false;
            }
            game.moveSelectedEntityTo(moveTo);

            // we only want it to move one time 
            entity.scriptState.hasActedThisRound = true;
            return true;
        }

        // we are unlocked, and we didnt find somethign to do , so we are done (return false)
        return false;
    } else if(turnState == TS_UnitAction_Move) {
        // wait until unlocked
        return true;
    } else if(turnState == TS_EntityAction_Target) {
        // we are targetting, should never happen when AI, wtf do we do?
        // return false as to not lock game
        return false;
    } else if(turnState == TS_EntityAction_Generic) {
        // we are currently attacking, so just wait it out
        return true;
    }

    // turn state is unknown, return false
    return false;
});

dummyScript;